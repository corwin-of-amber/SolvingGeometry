// Declarations
.decl Vpar(ray:symbol, vec:symbol)
.input Vpar

.decl MakeCircle(center: symbol, radius: symbol)
.output MakeCircle
.decl Circle(center: symbol, radius: symbol, id:symbol)
.input Circle
  
  
.decl Angle(a:symbol, b:symbol, c:symbol, angle:symbol)
.decl AngleCcw(a:symbol, b:symbol, c:symbol, angle:symbol)
.decl AngleCcwRays(ray0:symbol, ray1:symbol, angle:symbol)

.decl Perp(l1:symbol, l2:symbol)

.decl MakeRaythru(a:symbol, b:symbol)
.output MakeRaythru
.decl Raythru(a:symbol, b:symbol, id:symbol)
.input Raythru

.decl Origin(ray:symbol, a:symbol)

.decl MakeMinus(a:symbol, b:symbol)
.output MakeMinus

.decl Minus(a:symbol, b:symbol, id:symbol)
.input Minus

.decl AngleCcwVecs(u0:symbol,  u1:symbol, angle:symbol)

//TODO: Should I delete this?
.decl MakeLine(a:symbol, b:symbol)
.output MakeLine

.decl Dist(a: symbol, b: symbol, d:symbol)

.decl MakeIntersection(locus1: symbol, locus2: symbol)
.output MakeIntersection
.decl Intersection(locus1: symbol, locus2: symbol, id: symbol)
.input Intersection

//.decl SubsetOf(subset:symbol, superset:symbol)

.decl In(point: symbol, locus: symbol)
.input In
.output In

.decl MakeKnown(a: symbol)
.output MakeKnown
.decl Known(a: symbol)
.input Known
.output Known

// ApplyN is apply with N parameters
.decl  MakeApply1(id:symbol, str:symbol, a:symbol)
.output MakeApply1
.decl  MakeApply2(id:symbol, str:symbol, a:symbol, b:symbol)
.output MakeApply2
.decl  MakeApply3(id:symbol, str:symbol, a:symbol, b:symbol, c:symbol)
.output MakeApply3

.decl Dimension(id: symbol, dim: number)
.output Dimension

.decl KnownDimension(id: symbol, dim: number)
.output KnownDimension

.decl TypeOf(type:symbol, id:symbol)
.input TypeOf


// -- Circle --
MakeCircle(a, d):- Dist(a, _, d).

// -- Angle --
MakeRaythru(b,a):-AngleCcw(a, b, _, __).
MakeRaythru(b,c):-AngleCcw(_, b, c, __).
AngleCcwRays(ray0, ray1, angle):-AngleCcw(a, b, c, angle), Raythru(b,a,ray0), Raythru(b,c,ray1).
MakeKnown(angle):-AngleCcw(a, b, c, angle), Known(a), Known(b), Known(c).
MakeApply3(angle, "AngleCcw", a, b, c):-!Known(angle), AngleCcw(a, b, c, angle), Known(a), Known(b), Known(c).
        
Perp(l1, l2):-Angle(a,b,c,"90"), TypeOf("line", l1), TypeOf("line", l2), In(a, l1), In(c, l2), In(b, l1), In(b, l2).

MakeKnown(l):-TypeOf("line", l), In(a, l), Vpar(l, u), Known(a), Known(u).
MakeApply2(l, "Linevec", a, u):-!Known(l), TypeOf("line", l), In(a, l), Vpar(l, u), Known(a), Known(u).

// TODO: What if there aren't two points on the line?
MakeMinus(a, b):-TypeOf("line", l), In(a, l), In(b, l), a!=b.
Vpar(l, vec):-Minus(a, b, vec), TypeOf("line", l), In(a, l), In(b, l).

Perp(u, v):-Perp(l0, l1), Vpar(l0, u), Vpar(l1, v), TypeOf("line", l0), TypeOf("line", l1).

//TODO: Why is it Perp(v,u), and there isn't Perp(u,v)?
MakeKnown(v):-Perp(u, v), TypeOf("vec", v), Known(u).
MakeApply1(v, "Orth", u):-!Known(v), Perp(u, v), TypeOf("vec", v), Known(u).

// -- Ray --
// raythru definition
TypeOf("ray", ray), MakeMinus(a, b):-Raythru(a, b, ray).
TypeOf("vec", vec):-Minus(_, __, vec).
Vpar(ray, vec):-Minus(a, b, vec), Raythru(a, b, ray).
Origin(ray, a),In(a, ray), In(b, ray):-Raythru(a, b, ray).

MakeKnown(ray):-TypeOf("ray", ray), Origin(ray, a), Vpar(ray, u), Known(a), Known(u).
MakeApply2(ray, "Rayvec", a, u):-!Known(ray),TypeOf("ray", ray), Origin(ray, a), Vpar(ray, u), Known(a), Known(u).

//ray-rotate(ccw)
AngleCcwVecs(u0, u1, angle):-TypeOf("ray", ray0), TypeOf("ray", ray1), AngleCcwRays(ray0, ray1, angle), Vpar(ray0, u0), Vpar(ray1, u1).


//rotate(ccw)-1
MakeKnown(v):-AngleCcwVecs(u, v, angle), TypeOf("vec", v), Known(u), Known(angle).
MakeApply2(v, "Rotate(ccw)", u, angle):-!Known(v), AngleCcwVecs(u, v, angle), TypeOf("vec", v), Known(u), Known(angle).
MakeKnown(u):-AngleCcwVecs(u, v, angle), TypeOf("vec", u), Known(v), Known(angle).
MakeApply2(u, "Rotate(cw)", v, angle):-!Known(u), AngleCcwVecs(u, v, angle), TypeOf("vec", u), Known(v), Known(angle).

// -- Arithmetics rules --
Dist(a, b, d):- Dist(b, a, d).

// Create intersections only between objects that aren't both from dimension 0
MakeIntersection(c1, c2):-In(b, c1), In(b, c2), c1!=c2, !(Dimension(c1, 0); Dimension(c2, 0)).
//MakeIntersection(c1, c2):-In(b, c1), In(b, c2), c1!=c2,!SubsetOf(c1,c2), !SubsetOf(c2, c1).
//!(SubsetOf(l, c1); SubsetOf(l , c2)).
  
//SubsetOf(subset, superset):-Intersection(_, superset, subset).
//SubsetOf(subset, superset):-Intersection(superset, _, subset).
//SubsetOf(subset, superset):-SubsetOf(subset, l), SubsetOf(l, superset).

In(b, c):- Dist(b, a, d), Circle(a, d, c).
In(b, i):-In(b, c1), In(b, c2), Intersection(c1, c2, i).
  

MakeKnown(c):- Circle(a, d, c), Known(a), Known(d).
MakeApply2(c, "Circle", a, d):-!Known(c), Circle(a, d, c), Known(a), Known(d).

MakeKnown(i):- Known(c1), Known(c2), Intersection(c1, c2, i).
MakeApply2(i, "Intersection", c1, c2):-!Known(i), Known(c1), Known(c2), Intersection(c1, c2, i).

MakeKnown(dist):-Known(a), Known(b), Dist(a, b, dist).
MakeApply2(dist, "Dist", a, b):-!Known(dist),Known(a), Known(b), Dist(a, b, dist).

MakeKnown(vec):-Minus(a, b, vec), Known(a), Known(b).
MakeApply2(vec, "vec", a, b):-!Known(vec),Minus(a, b, vec), Known(a), Known(b).


Dimension(i, 1):-TypeOf("line", i).
Dimension(i, 1):-Circle(_, __, i).
Dimension(i, 1):-TypeOf("ray", i).
Dimension(i, 0):-Intersection(a, b, i), Dimension(a, 1), Dimension(b, 1), a!= b.

KnownDimension(id, dim):-Dimension(id, dim), Known(id).
